@echo off
set CONNECTION_TARGET_IP=192.168.10.11
set CMD_JAVA_WEB_START="C:\Program Files (x86)\Java\jre7\bin\javaws.exe" -Xnosplash http://192.168.10.11/apps/webCommunicator/index.jnlp
set MAX_COUNT_PING=0
set MAX_TIME=60

TITLE [サーバ:%CONNECTION_TARGET_IP%]への疎通確認＆WebCommunicator起動

:StartPing
cls
echo. -------------------- 注意！！ --------------------
echo. 処理中にウインドウは閉じないでください！！
echo. このウインドウは正常に処理が終わると自動で閉じます。
echo. エラーが発生した場合はエラーメッセージを
echo. 表示しますので内容をご確認頂き、
echo. 分からない場合はシステム管理者に相談してください。
echo. --------------------------------------------------
call :IsTimer StartTime
echo. [サーバ:%CONNECTION_TARGET_IP%]への疎通確認＆WebCommunicator起動処理を開始しました。
if %MAX_COUNT_PING% GTR 0 echo. 疎通確認上限回数は、%MAX_COUNT_PING%回に設定されています。
if %MAX_TIME% GTR 0 echo. 処理実行上限時間は、%MAX_TIME%秒に設定されています。
set LoopCounter=0

:LoopIn_1
call :IsTimer StopTime
set /a LoopCounter+=1
echo. [サーバ:%CONNECTION_TARGET_IP%]への疎通確認中...[%LoopCounter%回目]
for /f "usebackq" %%a in (`ping %CONNECTION_TARGET_IP% ^| findstr /i /c:TTL`) do set ResultPing=%%a
if defined ResultPing goto :Ping_OK
if %MAX_COUNT_PING% EQU 0 goto :LoopIn_1
if %LoopCounter% LSS %MAX_COUNT_PING% goto :LoopIn_1
echo. %LoopCounter%回疎通確認を行いましたが、確認ができませんでした。
goto :ERR_Ping

:Ping_OK
echo. [サーバ:%CONNECTION_TARGET_IP%]への疎通確認が%LoopCounter%回目で成功しました。
goto :Start_Javaws

:Start_Javaws
%CMD_JAVA_WEB_START%
echo. WebCommunicatorを起動しました。
exit

:ERR_Ping
echo. -------------------- エラーが発生しました！！ ------------------
echo. [サーバ:%CONNECTION_TARGET_IP%]への疎通確認に失敗しました。
echo.
echo. WebCommunicatorは起動していません。
echo. ネットワーク復旧後に、WebCommunicatorを手動で起動してください。
echo. ----------------------------------------------------------------
pause
exit

:IsTimer
if %MAX_TIME% GTR 0 call :%1
exit /b

:StartTime
SET T=%TIME: =0%
SET H=%T:~0,2%
SET M=%T:~3,2%
SET S=%T:~6,2%
SET L=%T:~9,2%

set /a H=1%H%-100
set /a M=1%M%-100
set /a S=1%S%-100
exit /b

:StopTime
SET T1=%TIME: =0%
SET H1=%T1:~0,2%
SET M1=%T1:~3,2%
SET S1=%T1:~6,2%
SET L1=%T1:~9,2%

set /a H1=1%H1%-100
set /a M1=1%M1%-100
set /a S1=1%S1%-100
SET /a H2=H1-H

SET /a M2=M1-M
if %M2% LSS 0 set /a H2=H2-1
if %M2% LSS 0 set /a M2=M2+60

SET /a S2=S1-S
if %S2% LSS 0 set /a M2=M2-1
if %S2% LSS 0 set /a S2=S2+60

SET /a L2=L1-L
if %L2% LSS 0 set /a S2=S2-1
if %L2% LSS 0 set /a L2=L2+100

SET /a DPS=%H2%*3600+%M2%*60+%S2%
SET DPS2=%DPS%.%L2%

set DPS_STAMP=%H2%:%M2%:%S2%
set DPS_STAMP2=%DPS_STAMP%.%L2%

echo. 処理開始から%DPS2%秒経過...
if %DPS% GEQ %MAX_TIME% goto :TimeOver
exit /b

:TimeOver
echo.
echo. 処理開始から%MAX_TIME%秒以上経過したため終了します。
goto :ERR_Ping
